document = { SOI ~ (chunk)* ~ EOI }

value = _{
    call
  | llvm_ir
  | integer
  | sized_string
  | string
  | read
  | identifier
  | ordered_comparison
}

identifier = @{ (ASCII_ALPHANUMERIC | "_" | "-" | ":" | "." | "%" | "*" | "!")+ }

sized_string =  { "\"" ~ inner ~ "\"" ~ pair_alignment }
string       = @{ "\"" ~ inner ~ "\"" }
inner        = @{ (!("\"" | "\\" | "\u{0000}" | "\u{001F}") ~ ANY)* ~ (escape ~ inner)? }
escape       = @{ "\\" ~ ("b" | "t" | "n" | "f" | "r" | "\"" | "\\" | NEWLINE)? }

integer = @{ ("+" | "-")? ~ int }
int     = @{ "0" | (ASCII_NONZERO_DIGIT ~ digits?) }
digits  = @{ (ASCII_DIGIT | ("_" ~ ASCII_DIGIT))+ }

WHITESPACE = _{ " " | "\t" | NEWLINE }
COMMENT    = _{ "//" ~ (!NEWLINE ~ ANY)* }

chunk = _{ include | function | no_alloca_pair | pair | pipe | return | for_loop | while_loop | conditional | value | section | block }

block    = { "{" ~ chunk* ~ "}" }
section  = { "<" ~ identifier ~ ">" ~ block }
param    = { type_annotation ~ identifier }
function = { identifier ~ identifier ~ "(" ~ (param ~ ","*)* ~ ")" ~ block }
return   = { "return" ~ call_param }

COMMA      = { "," }
call       = { identifier ~ "(" ~ (call_param ~ COMMA?)* ~ ")" }
call_param = { value ~ pair_alignment? ~ ("@" ~ identifier)? }

type_annotation = { identifier }
pair            = { int? ~ type_annotation ~ identifier ~ "=" ~ value }
pair_alignment  = { "<" ~ int ~ ">" }
read            = { "*" ~ identifier }
no_alloca_pair  = { identifier ~ ":=" ~ value }
pipe            = { identifier ~ "<|" ~ value }

llvm_ir = { "ll" ~ string }
include = { "#include" ~ string }

ordered_comparison    = { "(" ~ comparison ~ ")" }
GREATER_THAN          = { ">" }
LESS_THAN             = { "<" }
GREATER_THAN_EQUAL_TO = { ">=" }
LESS_THAN_EQUAL_TO    = { "<=" }
NOT_EQUAL             = { "!=" }
EQUAL                 = { "==" }
AND                   = { "&&" }
OR                    = { "||" }
comparison            = { value ~ (GREATER_THAN_EQUAL_TO | LESS_THAN_EQUAL_TO | GREATER_THAN | LESS_THAN | NOT_EQUAL | EQUAL | AND | OR) ~ value }

for_loop           = { "for" ~ "(" ~ pair ~ ";" ~ comparison ~ ")" ~ block }
while_loop         = { "while" ~ "(" ~ comparison ~ ")" ~ block }
conditional        = { "if" ~ comparison ~ block ~ (conditional_elseif | conditional_else)* }
conditional_else   = { "else" ~ block }
conditional_elseif = { "else if" ~ comparison ~ block }
