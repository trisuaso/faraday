document = { SOI ~ (chunk)* ~ EOI }

value = _{
    call
  | llvm_ir
  | integer
  | string
  | read
  | identifier
}

identifier = @{ (ASCII_ALPHANUMERIC | "_" | "-" | ":" | "." | "%" | "*")+ }

string = @{ "\"" ~ inner ~ "\"" }
inner  = @{ (!("\"" | "\\" | "\u{0000}" | "\u{001F}") ~ ANY)* ~ (escape ~ inner)? }
escape = @{ "\\" ~ ("b" | "t" | "n" | "f" | "r" | "\"" | "\\" | NEWLINE)? }

integer = @{ ("+" | "-")? ~ int }
int     = @{ "0" | (ASCII_NONZERO_DIGIT ~ digits?) }
digits  = @{ (ASCII_DIGIT | ("_" ~ ASCII_DIGIT))+ }

WHITESPACE = _{ " " | "\t" | NEWLINE }
COMMENT    = _{ "//" ~ (!NEWLINE ~ ANY)* }

chunk = _{ function | no_alloca_pair | pair | pipe | return | value | section | block }

block    = { "{" ~ chunk* ~ "}" }
section  = { "<" ~ identifier ~ ">" ~ block }
param    = { type_annotation ~ identifier }
function = { identifier ~ identifier ~ "(" ~ (param ~ ","*)* ~ ")" ~ block }

COMMA      = { "," }
call       = { identifier ~ "(" ~ (call_param ~ COMMA?)* ~ ")" }
call_param = { value ~ ("@" ~ identifier)? }

type_annotation = { identifier }
pair            = { int? ~ type_annotation ~ identifier ~ "=" ~ value }
no_alloca_pair  = { identifier ~ ":=" ~ value }
pipe            = { identifier ~ "<|" ~ value }
llvm_ir         = { "ll" ~ string }
return          = { "return" ~ value }
read            = { "*" ~ identifier }
